import hudson.scm.ChangeLogSet
import hudson.plugins.git.GitChangeSet

properties([buildDiscarder(logRotator(artifactDaysToKeepStr: '',
                                      artifactNumToKeepStr: '',
                                      daysToKeepStr: '7',
                                      numToKeepStr: '14')),
            disableConcurrentBuilds(),
            disableResume(),
            pipelineTriggers([cron('06 16 * * 1-5')]),
            pipelineTriggers([pollSCM('H/5 * * * *')])])

node {
    def kaynnistaja
    // Tätä Build User Vars Plugin plugaria ei tarvisi, jos JENKINS-41272 issue saataisiin jossain väli tehtyä
    wrap([$class: 'BuildUser']) {
        kaynnistaja = sh([script      : 'echo ${BUILD_USER}',
                          returnStdout: true])
    }
    println "KÄYNNISTÄJÄ: " + kaynnistaja
    // Pitää wrapata koko pipeline tämmöisen funktion taakse, koska git polling plugarissa on bugi JENKINS-30350
    // ja lisäksi se pollailee kaikkia repoja JENKINS-41674.
    if (muutosTapahtuiHarjaan(kaynnistaja)) {
        // Lisää mvn pathiin
        env.PATH = "${env.JENKINS_HOME}/tools/hudson.tasks.Maven_MavenInstallation/Maven_3.5.4/bin:${env.PATH}"

        // Stage nimet
        String testikannanLuonti = 'Luo testikanta'
        String jarJaTestit = 'Luo JAR ja aja testit'
        String testiserverinKannanLuonti = 'Luo testiserverin kanta'
        String testiserverinAppLuonti = 'Luo testiserverin app'
        String e2eTestit = 'Aja E2E testit'
        String stagingserverinKannanLuonti = 'Luo stageserverin kanta'
        String stagingserverinAppLuonti = 'Luo stageserverin app'

        // Mitkä staget ajetaan
        Boolean testikannanLuontiAjetaan = STAGE_ETIAPAIN == "Kaikki"
        Boolean jarJaTestitAjetaan = ["Kaikki", "JAR ja testit"].contains(STAGE_ETIAPAIN)
        Boolean testiserverinKannanLuontiAjetaan = ["Kaikki", "JAR ja testit", "Testiserveri"].contains(STAGE_ETIAPAIN)
        Boolean testiserverinAppLuontiAjetaan = ["Kaikki", "JAR ja testit", "Testiserveri"].contains(STAGE_ETIAPAIN)
        Boolean e2eTestitAjetaan = ["Kaikki", "JAR ja testit", "Testiserveri", "E2E"].contains(STAGE_ETIAPAIN)
        Boolean stagingserverinKannanLuontiAjetaan = ["Kaikki", "JAR ja testit", "Testiserveri", "Stagingserveri"].contains(STAGE_ETIAPAIN)
        Boolean stagingserverinAppLuontiAjetaan = ["Kaikki", "JAR ja testit", "Testiserveri", "Stagingserveri"].contains(STAGE_ETIAPAIN)

        dir('Harja') {
            checkout scm;
            /*
             * TESTIKANNAN LUOMINEN
             */

            stage(testikannanLuonti) {
                if (testikannanLuontiAjetaan) {
                    try {
                        sh([script: "sh Jenkins/skriptit/testitietokanta.sh"])
                        hoidaMahdollisenErrorinKorjaantuminen(testikannanLuonti, "Pipeline ei enää hajoa Jenkinsin testikannan luomiseen")
                    } catch (e) {
                        hoidaErrori(testikannanLuonti, "Pipeline hajosi Jenkinsin testikannan luomiseen")
                    }
                }
            }

            /*
             * TESTIEN AJAMINEN JA JAR:IN LUOMINEN
             */

            stage(jarJaTestit) {
                if (jarJaTestitAjetaan) {
                    try {
                        // Luo API docsit
                        sh([script: "sh Jenkins/skriptit/luo-API-docsit.sh"])
                        // Luo jarri ja aja testit
                        sh([script: "lein tuotanto"])
                        // Säilötään se jarri
                        archiveArtifacts([artifacts: 'target/harja-*-standalone.jar, doc/*'])
                        hoidaMahdollisenErrorinKorjaantuminen(jarJaTestit, "Pipeline ei enää hajoa testien ajamiseen/JAR:in luomiseen")
                    } catch (e) {
                        String muutokset = changeSets2String()
                        mail([from   : params.LAHETTAJA_SPOSTI,
                              replyTo: '',
                              to     : params.VASTAANOTTAJAT_SPOSTI,
                              cc     : '',
                              bcc    : '',
                              subject: "Pipelinen ajaminen epäonnistui ${env.BUILD_NUMBER}",
                              body   : "Build: ${env.BUILD_URL}\n" + muutokset])
                        hoidaErrori(jarJaTestit, "Pipeline hajosi testien ajamiseen/JAR:in luomiseen")
                    } finally {
                        // Testitulokset
                        junit([testResults: 'test2junit/xml/*.xml'])
                    }
                }
            }

            /*
             * TESTISERVERIKANNAN LUOMINEN
             */

            stage(testiserverinKannanLuonti) {
                if (testiserverinKannanLuontiAjetaan) {
                    try {
                        withCredentials([usernamePassword(credentialsId: 'TESTIPANNU', passwordVariable: 'SALASANA', usernameVariable: 'KAYTTAJA')]) {
                            // Halutessaan withMaven:in avulla voi määritellä conffi filet, mutta käytetään defaultteja
                            withMaven(jdk: '', maven: 'Maven 3.5.4') {
                                sh([script: "mvn -f tietokanta/pom.xml clean compile flyway:migrate" +
                                        " -Dflyway.baselineOnMigrate=true -Dflyway.baselineVersion=0" +
                                        " -Dflyway.url=jdbc:postgresql://harja-db1-test/harja -Dflyway.user=$KAYTTAJA -Dflyway.password=$SALASANA"])
                            }
                        }
                        hoidaMahdollisenErrorinKorjaantuminen(testiserverinKannanLuonti, "Pipeline ei enää hajoa testiserverin kannan luomiseen")
                    } catch (e) {
                        hoidaErrori(testiserverinKannanLuonti, "Pipeline hajosi testiserverin kannan luomiseen")
                    }
                }
            }
        }

        def jenkinsinBuildNumber
        if (params.ARCHIVE_BUILD_NUMBER) {
            slackSend([color  : 'warning',
                       message: 'Käytetään JAR:ia buildista: ' + params.ARCHIVE_BUILD_NUMBER])
            jenkinsinBuildNumber = params.ARCHIVE_BUILD_NUMBER
        } else {
            jenkinsinBuildNumber = currentBuild.number
        }
        if (!onkoTiedostoOlemassa("${env.JENKINS_HOME}/jobs/Harja-pipeline/builds/" + jenkinsinBuildNumber + "/archive/target/harja-*-standalone.jar")) {
            slackSend([color  : 'warning',
                       message: 'JAR:ia ei löytynyt halutulla numerolla: ' + jenkinsinBuildNumber])
            error([message: "JAR:ia ei löytynyt halutulla numerolla"])
        }

        dir('CI') {
            checkout([$class                           : 'GitSCM',
                      branches                         : [[name: '*/master']],
                      doGenerateSubmoduleConfigurations: false,
                      extensions                       : [],
                      submoduleCfg                     : [],
                      userRemoteConfigs                : [[url: params.HARJA_CI_URL]]])
            /*
             * TESTISERVERIAPPIKSEN DEPLOYAAMINEN
             */

            stage(testiserverinAppLuonti) {
                if (testiserverinAppLuontiAjetaan) {
                    try {
                        ansiblePlaybook([installation: 'ansible 1.9.1',
                                         inventory   : 'inventory/testing',
                                         playbook    : 'playbooks/nightly.yml',
                                         extraVars   : [
                                                 jenkins_build_number: jenkinsinBuildNumber
                                         ]])
                        hoidaMahdollisenErrorinKorjaantuminen(testiserverinAppLuonti, "Pipeline ei enää hajoa testiserverin appiksen luomiseen")
                    } catch (e) {
                        hoidaErrori(testiserverinAppLuonti, "Pipeline hajosi testiserverin appiksen luomiseen")
                    }
                }
            }
        }

        dir('E2E') {
            checkout([$class                           : 'GitSCM',
                      branches                         : [[name: '*/develop']],
                      doGenerateSubmoduleConfigurations: false,
                      extensions                       : [],
                      submoduleCfg                     : [],
                      userRemoteConfigs                : [[url: params.HARJA_E2E_URL]]])

            /*
             * E2E TESTIT
             */
            stage(e2eTestit) {
                if (e2eTestitAjetaan) {
                    try {
                        wrap([$class: 'Xvfb']) {
                            retry(5) {
                                timeout(20) {
                                    sh([script: "lein do clean, compile, test2junit"])
                                }
                            }
                        }
                        hoidaMahdollisenErrorinKorjaantuminen(e2eTestit, "Pipeline ei enää hajoa E2E testeihin")
                    } catch (e) {
                        hoidaErrori(e2eTestit, "Pipeline hajosi E2E testeihin")
                    } finally {
                        junit([testResults: 'test2junit/xml/*.xml'])
                    }
                }
            }
        }

        dir('Harja') {
            /*
             * STAGESERVERIKANNAN LUOMINEN
             */

            stage(stagingserverinKannanLuonti) {
                if (stagingserverinKannanLuontiAjetaan) {
                    try {
                        withCredentials([usernamePassword(credentialsId: 'STAGEPANNU', passwordVariable: 'SALASANA', usernameVariable: 'KAYTTAJA')]) {
                            // Halutessaan withMaven:in avulla voi määritellä conffi filet, mutta käytetään defaultteja
                            withMaven(jdk: '', maven: 'Maven 3.5.4') {
                                sh([script: "mvn -f tietokanta/pom.xml clean compile flyway:migrate" +
                                        " -Dflyway.baselineOnMigrate=true -Dflyway.baselineVersion=0" +
                                        " -Dflyway.url=jdbc:postgresql://harja-db1-stg/harja -Dflyway.user=$KAYTTAJA -Dflyway.password=$SALASANA"])
                            }
                        }
                        hoidaMahdollisenErrorinKorjaantuminen(stagingserverinKannanLuonti, "Pipeline ei enää hajoa stagingserverin kannan luomiseen")
                    } catch (e) {
                        hoidaErrori(stagingserverinKannanLuonti, "Pipeline hajosi stagingserverin kannan luomiseen")
                    }
                }
            }
        }

        dir('CI') {
            /*
             * STAGING SERVERIAPPIKSEN DEPLOYAAMINEN
             */

            stage(stagingserverinAppLuonti) {
                if (stagingserverinAppLuontiAjetaan) {
                    try {
                        ansiblePlaybook([installation: 'ansible 1.9.1',
                                         inventory   : 'inventory/staging',
                                         playbook    : 'playbooks/staging.yml',
                                         extraVars   : [
                                                 jenkins_build_number: jenkinsinBuildNumber
                                         ]])
                        hoidaMahdollisenErrorinKorjaantuminen(stagingserverinAppLuonti, "Pipeline ei enää hajoa stagingserverin appiksen luomiseen")
                    } catch (e) {
                        hoidaErrori(stagingserverinAppLuonti, "Pipeline hajosi stagingserverin appiksen luomiseen")
                    }
                }
            }
        }

        /*
         * SIIVOTAAN WORKSPACE
         */

        stage('Siivoa workspace') {
            cleanWs()
        }
    }
}

def hoidaMahdollisenErrorinKorjaantuminen(stageNimi, viesti='Pipeline korjaantui') {
    // Jos edellinen buildi hajosi ja tämä buildi korjasi sen, niin lähetetään viesti Slackiin
    if (currentBuild.previousBuild.buildVariables.FAILED_STAGE == stageNimi) {
        slackSend([color  : 'good',
                   message: viesti])
    }
}

def hoidaErrori(stageNimi, viesti='Pipelinessä tapahtui poikkeus') {
    env.FAILED_STAGE = stageNimi
    slackSend([color  : 'warning',
               message: viesti])
    error([message: stageNimi + " epäonnistui."])
}

@NonCPS
def changeSets2String() {
    def muutokset = currentBuild.changeSets
    def teksti = ""
    for (ChangeLogSet muutos : muutokset) {
        for (GitChangeSet kentta : muutos.getItems()) {
            def tekija
            def viesti
            try {
                tekija = kentta.getAuthor()
            } catch (e) {
                tekija = "Unknown"
            }
            try {
                viesti = kentta.getMsg()
            } catch (e) {
                viesti = "Unkown"
            }
            teksti = teksti + "[" + tekija + "] " + viesti + "\n"
        }
    }
    return teksti
}

def onkoTiedostoOlemassa(absolutePath) {
    loytyikoTiedosto = sh([script      : "[ -f " + absolutePath + " ]",
                           returnStatus: true])
    return loytyikoTiedosto == 0
}

def hyvaksyttyPolku(polku) {
    // Elikkä git pollaus pollailee kaikkia tässä filussa checkoutattuihin repoihin.
    // Määrritellään tässä, että kun muutos tapahtuu jossain tietyssä polussa, niin ajetaan
    // putki vain silloin.
    def hyvaksytytPolut = ["src/", "resources/", "Jenkins/", "test/", "tietokanta/"]
    def eiHyvaksytytPolut = ["src/harja/e2e/"]
    def hyvaksyttyPolkub = false
    for (String hp : hyvaksytytPolut) {
        def hpr = "^" + hp.replaceAll(~/\//, "\\\\/")
        def hpm = (polku =~ ~hpr)
        if (hpm.getCount() != 0) {
            for (String ehp : eiHyvaksytytPolut) {
                def ehpr = "^" + ehp.replaceAll(~/\//, "\\\\/")
                def ehpm = (polku =~ ~ehpr)
                if (ehpm.getCount() != 0) {
                    return false
                }
            }
            hyvaksyttyPolkub = true
        }
    }
    return hyvaksyttyPolkub
}

@NonCPS
def muutosTapahtuiHarjaan(kaynnistaja) {
    println "KÄYNNISTÄJÄ: " + kaynnistaja
    def muutokset = currentBuild.changeSets
    println "MUUTOKSET: " + muutokset
    println "TYHYJÄ: " + muutokset.isEmpty()
    println kaynnistaja == "SCMTrigger"
    // Onko pollaus lauennu turhan takia
    if (muutokset.isEmpty() && kaynnistaja == "SCMTrigger") {
        return false
    } else if (muutokset.isEmpty()) {
        // Tämä tarkoittaa sitä, että joku käyttäjä on käynyt painamassa erikseen sitä nappia, että työ pitäs käynistää
        println "Käyttäjä triggeröi"
        return true
    } else {
        // Tarkistetaan polku
        for (ChangeLogSet muutos : muutokset) {
            for (GitChangeSet kentta : muutos.getItems()) {
                def polut
                try {
                    polut = kentta.getAffectedPaths()
                } catch (e) {
                    continue
                }
                for (String polku : polut) {
                    if (hyvaksyttyPolku(polku)) {
                        println "HYVÄKSYTTY POLKU: " + polku
                        return true
                    }
                }
            }
        }
        return false
    }
}
